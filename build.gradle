allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "java"
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

def subshadowRE = /robolectric-shadows\/shadows-core\/v(.*)$/
configure(subprojects.findAll { project -> project.name =~ subshadowRE }) {
    def m = project.name =~ subshadowRE
    def androidApi = Integer.parseInt(m[0][1])
    println "androidApi is $androidApi"

    apply plugin: "java"
    apply plugin: ShadowsPlugin
    apply plugin: ProvidedPlugin
    apply plugin: SubShadowsPlugin

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    println("declaring androidApi == $androidApi in ${project}")
    def robolectricVersion = AndroidSdk.versions[androidApi]
    logger.info "[${project.name}] Android API is $androidApi — $robolectricVersion"
    println "[${project.name}] Android API is $androidApi — $robolectricVersion"

    shadows {
        packageName "org.robolectric"
    }

    subshadows {
        apiLevel androidApi
    }

    generateShadowProvider {
        dependsOn velocity
    }

    dependencies {
        // Project dependencies
        compile project(":robolectric-annotations")
        compile project(":robolectric-resources")
        compile project(":robolectric-utils")

        // Compile dependencies
        compile "com.intellij:annotations:12.0"
        compile "com.almworks.sqlite4java:sqlite4java:0.282"
        provided("org.robolectric:android-all:$robolectricVersion") { force = true }
        compile "com.ibm.icu:icu4j:53.1"

        runtime "com.github.axet.litedb:libsqlite:0.282-3:natives-mac-x86_64"
        runtime "com.github.axet.litedb:libsqlite:0.282-3:natives-linux-x86"
        runtime "com.github.axet.litedb:libsqlite:0.282-3:natives-linux-x86_64"
        runtime "com.github.axet.litedb:libsqlite:0.282-3:natives-windows-x86"
        runtime "com.github.axet.litedb:libsqlite:0.282-3:natives-windows-x86_64"

        // Testing dependencies
        testCompile "junit:junit:4.8.2"
        testCompile "org.hamcrest:hamcrest-core:1.3"
        testCompile "org.assertj:assertj-core:2.0.0"
    }


}