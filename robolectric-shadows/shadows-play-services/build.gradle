apply plugin: "java"
apply from: "../../provided.gradle"

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    shadows
}

sourceSets.main.java.srcDirs += "${buildDir}/generated-shadows"

task generateShadows(type: JavaCompile, description: "Generate shadow classes") {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.shadows
    options.compilerArgs = [
            "-proc:only",
            "-processor", "org.robolectric.annotation.processing.RobolectricProcessor",
            "-Aorg.robolectric.annotation.processing.shadowPackage=org.robolectric.shadows.gms"
    ]
    destinationDir = file("$buildDir/generated-shadows")
}

compileJava {
    dependsOn generateShadows
}

dependencies {
    compile project(":robolectric")
    compile project(":robolectric-shadows/shadows-core")

    // Run the annotation processor
    shadows project(":robolectric-processor")

    compile "com.android.support:support-v4:23.2.0"
    compile "com.google.android.gms:play-services-base:8.4.0"
    compile "com.google.android.gms:play-services-basement:8.4.0"

    // Testing dependencies
    testCompile "junit:junit:4.8.2"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.assertj:assertj-core:2.0.0"
    testCompile "org.mockito:mockito-core:1.8.0"
}
